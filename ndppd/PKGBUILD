# Maintainer: Tosainu <tosainu.maple@gmail.com>

pkgname=ndppd
pkgver=0.2.6.r9.g4f0301f
pkgrel=2
pkgdesc='NDP Proxy Daemon'
arch=(
  aarch64
  x86_64
)
url='https://github.com/DanielAdolfsson/ndppd'
license=(GPL-3.0-or-later)
depends=()
makedepends=(
  git
)
backup=('etc/ndppd.conf')
_commit=4f0301f94345c92b723392ed2d73f04f910e6c30
source=(git+https://github.com/DanielAdolfsson/ndppd.git#commit=${_commit})
sha256sums=('SKIP')

pkgver() {
  cd $pkgname

  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  sed -i "s/^\\(#define NDPPD_VERSION \\).*$/\\1\"$pkgver\"/" src/ndppd.h
  sed -i 's!/var/!/!g' ndppd.service

  make PREFIX=/usr all
}

package() {
  cd $pkgname

  make PREFIX=/usr SBINDIR=/${pkgdir}/usr/bin DESTDIR=${pkgdir} install
  install -D -m 644 ndppd.service "${pkgdir}/usr/lib/systemd/system/ndppd.service"
  install -D -m 644 ndppd.conf-dist "${pkgdir}/etc/ndppd.conf"
}
