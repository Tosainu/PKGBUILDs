# Maintainer: Tosainu <tosainu.maple@gmail.com>

pkgname=ndppd
pkgver=0.2.6.r9.g4f0301f
pkgrel=3
pkgdesc='NDP Proxy Daemon'
arch=(
  aarch64
  x86_64
)
url='https://github.com/DanielAdolfsson/ndppd'
license=(GPL-3.0-or-later)
depends=()
makedepends=(
  git
)
backup=('etc/ndppd.conf')
_commit=4f0301f94345c92b723392ed2d73f04f910e6c30
source=(git+https://github.com/DanielAdolfsson/ndppd.git#commit=${_commit}
        ndppd.service)
sha256sums=('SKIP'
            'f3bf0bee32149d8c773747aa767e0f4b513d0a819e90fe1a5aae64f5caa83c65')

pkgver() {
  cd $pkgname

  git describe --long --tags --abbrev=7 | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd $pkgname

  sed -i "s/^\\(#define NDPPD_VERSION \\).*$/\\1\"$pkgver\"/" src/ndppd.h

  make PREFIX=/usr all
}

package() {
  cd $pkgname

  make PREFIX=/usr SBINDIR=/${pkgdir}/usr/bin DESTDIR=${pkgdir} install
  install -D -m 644 "${srcdir}/ndppd.service" "${pkgdir}/usr/lib/systemd/system/ndppd.service"
  install -D -m 644 ndppd.conf-dist "${pkgdir}/etc/ndppd.conf"
}
